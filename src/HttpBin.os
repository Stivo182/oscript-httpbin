#Использовать autumn
#Использовать winow

Перем Поделка;
Перем ВебСервер;
Перем НастройкиВебСервера;
Перем СлушательПорта;
Перем ОжидатьЗапуск;

#Область ПрограммныйИнтерфейс

// Запускает сервис
Процедура Старт() Экспорт
	ФоновыеЗадания.Выполнить(ВебСервер, "Старт");

	Если ОжидатьЗапуск Тогда
		ОжидатьЗапуск();
	КонецЕсли;
КонецПроцедуры

// Останавливает сервис
Процедура Стоп() Экспорт
	ВебСервер.Стоп();
КонецПроцедуры

// Устанавливает задержку перед чтением сокета
//
// Параметры:
//   Задержка - Число - Задержка в миллисекундах (По умолчанию 65 мс)
Процедура УстановитьЗадержкуПередЧтениемСокета(Задержка) Экспорт
	НастройкиВебСервера.ЗадержкаПередЧтениемСокета = Задержка;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриСозданииОбъекта(АдресУзла = "127.0.0.1:3333", ОжидатьЗапускСервиса = Истина)
	
	ОжидатьЗапуск = ОжидатьЗапускСервиса;

	Поделка = Новый Поделка;
	Поделка.ЗапуститьПриложение();

	ВебСервер = Поделка.НайтиЖелудь("ВебСервер");
	НастройкиВебСервера = Поделка.НайтиЖелудь("Настройки");
	СлушательПорта = Поделка.НайтиЖелудь("СлушательПорта");

	ЧастиАдресаУзла = РазделитьАдресУзла(АдресУзла);

	НастройкиВебСервера.РазмерБуфера = 0;
	НастройкиВебСервера.ИмяХоста = ЧастиАдресаУзла.Хост;
	НастройкиВебСервера.Порт = ЧастиАдресаУзла.Порт;

	УстановитьЗадержкуПередЧтениемСокета(65);

КонецПроцедуры

Процедура ОжидатьЗапуск()

	Задержка = 100;
	МаксКоличествоИтераций = 10;
	НомерИтерации = 0;

	Пока Не СлушательПорта.Активен() Цикл
		НомерИтерации = НомерИтерации + 1;
		Приостановить(Задержка);

		Если НомерИтерации = МаксКоличествоИтераций Тогда
			Стоп();
			ВызватьИсключение СтрШаблон(
				"Не удалось запустить веб-сервер по адресу %1:%2 в течение %3 мс.",
				НастройкиВебСервера.ИмяХоста,
				НастройкиВебСервера.Порт,
				Задержка * МаксКоличествоИтераций);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция РазделитьАдресУзла(АдресУзла)
	
	Результат = Новый Структура("Хост, Порт", "", 80);

	ИндексДвоеточия = СтрНайти(АдресУзла, ":", НаправлениеПоиска.СНачала);

	Если ИндексДвоеточия Тогда
		Результат.Хост = Сред(АдресУзла, 1, ИндексДвоеточия - 1);
		Результат.Порт = Число(Сред(АдресУзла, ИндексДвоеточия + 1));
	Иначе
		Результат.Хост = АдресУзла;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти