Перем _ОжиданиеМеждуПроверками; // Число
Перем _ФоновоеЗадание; // ФоновоеЗадание, Неопределено

&Желудь
Процедура ПриСозданииОбъекта()
	_ОжиданиеМеждуПроверками = 5000;
КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Запускает фоновое наблюдение за процессом с указанным идентификатором.
// Если процесс завершается, текущий процесс также будет автоматически завершен.
//
// Параметры:
//  PID - Число - Идентификатор процесса, за которым необходимо наблюдать
Процедура НачатьНаблюдение(PID) Экспорт

	Если Не _ФоновоеЗадание = Неопределено 
		И _ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
		ВызватьИсключение "Наблюдение за родительским процессом уже запущено";
	КонецЕсли;

	ПараметрыМетода = Новый Массив();
	ПараметрыМетода.Добавить(PID);

	_ФоновоеЗадание = ФоновыеЗадания.Выполнить(ЭтотОбъект, "ПериодическаяПроверкаПроцесса", ПараметрыМетода);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПериодическаяПроверкаПроцесса(PID) Экспорт

	Пока Истина Цикл
		ЗавершитьЕслиПроцессОтсутствует(PID);
		Приостановить(_ОжиданиеМеждуПроверками);
	КонецЦикла;

КонецПроцедуры

Процедура ЗавершитьЕслиПроцессОтсутствует(PID)
			
	Процесс = НайтиПроцессПоИдентификатору(PID);
	Если Процесс = Неопределено Тогда
		ТекущийПроцесс().Завершить();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти