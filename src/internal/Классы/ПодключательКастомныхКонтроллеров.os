#Использовать fs

#Область ПрограммныйИнтерфейс

// Находит и подключает кастомные контроллеры из путей, указанных в аргументах командной строки.
//
Процедура НайтиИПодключить() Экспорт
	
	МассивФайлов = НайтиФайлыИзПереданныхАргументов();

	Для Каждого ПолноеИмя Из МассивФайлов Цикл

		Путь = ФС.НормализоватьПуть(ПолноеИмя);

		Если ФС.КаталогСуществует(Путь) Тогда
			ПодключитьИзКаталога(Путь);
		ИначеЕсли ФС.ФайлСуществует(Путь) Тогда
			Подключить(Путь);
		Иначе
			ВызватьИсключение СтрШаблон("Не удалось подключить контроллер '%1': файл или каталог не существует", ПолноеИмя);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Подключает контроллер из указанного файла.
//
// Параметры:
//   ПолноеИмя - Строка - Полный путь к файлу контроллера (.os)
//
Процедура Подключить(ПолноеИмя) Экспорт

	Имя = Новый Файл(ПолноеИмя).ИмяБезРасширения;

	Попытка
		ПодключитьСценарий(ПолноеИмя, Имя);
	Исключение
		ВызватьИсключение СтрШаблон(
			"Не удалось подключить контроллер '%1': %2", 
			ПолноеИмя, 
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке())
		);
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодключитьИзКаталога(Каталог)

	Файлы = НайтиФайлы(Каталог, "*.os");

	Для Каждого Файл Из Файлы Цикл
		Подключить(Файл.ПолноеИмя);
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиФайлыИзПереданныхАргументов()

	Файлы = Новый Массив();

	НайденКлюч = Ложь;
	Для Каждого Значение Из АргументыКоманднойСтроки Цикл

		Если НайденКлюч Тогда
			Файлы.Добавить(Значение);
			НайденКлюч = Ложь;
			Продолжить;
		КонецЕсли;

		Если Значение = "--routes-handlers" Тогда
			НайденКлюч = Истина;
		КонецЕсли;

	КонецЦикла;

	Возврат Файлы;

КонецФункции

#КонецОбласти